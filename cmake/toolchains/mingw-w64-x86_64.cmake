set(CMAKE_SYSTEM_NAME Windows)
set(CMAKE_SYSTEM_PROCESSOR x86_64)

set(THINGINO_MINGW_TRIPLET "x86_64-w64-mingw32" CACHE STRING "MinGW-w64 triplet used for cross-compiling Thingino Cloner")

set(CMAKE_C_COMPILER "${THINGINO_MINGW_TRIPLET}-gcc")
set(CMAKE_RC_COMPILER "${THINGINO_MINGW_TRIPLET}-windres")

set(_thingino_default_root "/usr/${THINGINO_MINGW_TRIPLET}")
if (EXISTS "${_thingino_default_root}")
    list(APPEND CMAKE_FIND_ROOT_PATH "${_thingino_default_root}")
endif()

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

get_filename_component(THINGINO_PROJECT_ROOT "${CMAKE_CURRENT_LIST_DIR}/../.." ABSOLUTE)
set(THINGINO_DEFAULT_LIBUSB_ROOT "${THINGINO_PROJECT_ROOT}/third_party/libusb/windows/x86_64")

if (EXISTS "${THINGINO_DEFAULT_LIBUSB_ROOT}")
    list(APPEND CMAKE_FIND_ROOT_PATH "${THINGINO_DEFAULT_LIBUSB_ROOT}")
    if (NOT DEFINED LIBUSB_ROOT OR LIBUSB_ROOT STREQUAL "")
        set(LIBUSB_ROOT "${THINGINO_DEFAULT_LIBUSB_ROOT}" CACHE PATH "Preferred libusb installation root" FORCE)
    endif()
endif()
